import os
import settings


################################ Scons Settings  #######################################

#Scons settings Section
env = Environment(ENV = os.environ, SHELL = '/bin/bash')
env.AppendENVPath('PATH', os.getcwd())
Decider('timestamp-newer')



######################################################################################################
###########################     Arguments     ########################################################
######################################################################################################

arguments_dictionary = {}

#Filename of the reference human genome
reference = ARGUMENTS.get("reference", settings.reference)
arguments_dictionary["reference"] = reference

#The reference directory should contain the fasta file with the human genome and the index build 
#by bwa with the bwa index <genome.fasta> command (it produces the a bunch of files with these extensions:
# - .amb
# - .ann
# - .bwt
# - .pac
# - .sa

# - reference.fa.fai obtained by samtools faidx <reference.fa>
# - reference.dict obtained by java -jar CreateSequenceDictionary.jar R=reference.fasta O=reference.dict
referenceDir = ARGUMENTS.get("referenceDir", settings.referenceDir)
if not referenceDir.endswith("/"): referenceDir = referenceDir + "/"
arguments_dictionary["referenceDir"] = referenceDir


annotationDir = ARGUMENTS.get("annotationDir",settings.annotationDir)
if not annotationDir.endswith("/"): annotationDir = annotationDir + "/"
arguments_dictionary["annotationDir"] = annotationDir = annotationDir

dbsnpVCF = ARGUMENTS.get("dbsnpVCF", settings.dbsnpVCF)
arguments_dictionary["dbsnpVCF"] = dbsnpVCF


exomeRegions = ARGUMENTS.get("exomeRegions", settings.exomeRegions)
arguments_dictionary["exomeRegions"] = exomeRegions

tumorBam = ARGUMENTS.get("tumorBam", settings.tumorBam)
arguments_dictionary["tumorBam"] = tumorBam
normalBam = ARGUMENTS.get("normalBam", settings.normalBam)
arguments_dictionary["normalBam"] = normalBam

startChr = int(ARGUMENTS.get("startChr", settings.startChr))
arguments_dictionary["startChr"] = startChr

endChr = int(ARGUMENTS.get("endChr", settings.endChr))
arguments_dictionary["endChr"] = endChr

snpeffDir = ARGUMENTS.get("snpeffDir",settings.snpeffDir)
if not snpeffDir.endswith("/"): snpeffDir = snpeffDir + "/"
arguments_dictionary["snpeffDir"] = snpeffDir

snpeffGenomeVersion = ARGUMENTS.get("snpeffGenomeVersion", settings.snpeffGenomeVersion)
arguments_dictionary["snpeffGenomeVersion"] = snpeffGenomeVersion

clinvarVCF = ARGUMENTS.get("clinvarVCF", settings.clinvarVCF)
arguments_dictionary["clinvarVCF"] = clinvarVCF

cosmicCodingVCF = ARGUMENTS.get("cosmicCodingVCF", settings.cosmicCodingVCF)
arguments_dictionary["cosmicCodingVCF"] = cosmicCodingVCF
cosmicNonCodingVCF = ARGUMENTS.get("cosmicNonCodingVCF", settings.cosmicNonCodingVCF)
arguments_dictionary["cosmicNonCodingVCF"] = cosmicNonCodingVCF


######################################################################################################
########################### Split input bams by chromosome ###########################################
######################################################################################################


samtools_split_cmd = "samtools view -b {} {} > $TARGET"

samtools_index_cmd = "samtools index $SOURCE"


command_list = []
for chr in range(startChr, endChr + 1):
    prefix = "t_"
    split_tumor_bam = env.Command(["t_chr{}.bam".format(chr)], [], samtools_split_cmd.format(tumorBam, chr))
    command_list.append(split_tumor_bam)
    index_bam = env.Command(["t_chr{}.bam.bai".format(chr)], [split_tumor_bam], samtools_index_cmd)
    command_list.append(index_bam)

for chr in range(startChr, endChr + 1):
    prefix = "n_"
    split_normal_bam = env.Command(["n_chr{}.bam".format(chr)], [], samtools_split_cmd.format(normalBam, chr))
    command_list.append(split_normal_bam)
    index_bam = env.Command(["n_chr{}.bam.bai".format(chr)], [split_normal_bam], samtools_index_cmd)
    command_list.append(index_bam)
    



######################################################################################################
########################### Run MuTect2 on every chromosome ##########################################
######################################################################################################

vcf_commands = []

mutect_command_part1 = "docker run -it --rm -v $$(pwd):/data"
mutect_command_part2 = "-v {referenceDir}:/genome -v {annotationDir}:/annotation alexcoppe/gatk -T MuTect2"
mutect_command_part3 = "-R /genome/{reference} -I:tumor /data/t_chr{chr}.bam -I:normal /data/n_chr{chr}.bam"
mutect_command_part4 = "-L /annotation/{exomeRegions} --dbsnp /annotation/{dbsnpVCF}  -o /data/$TARGET"
mutect_command_parts = [mutect_command_part1, mutect_command_part2, mutect_command_part3, mutect_command_part4]

for chr in range(startChr, endChr + 1):
    arguments_dictionary["chr"] = chr
    mutect2 = env.Command(["mutect_variants_chr{chr}.vcf".format(**arguments_dictionary)], [command_list], " ".join(mutect_command_parts).format(**arguments_dictionary))

    
    ######## Filter PASS variants #########################################
    filter_pass_variants_cmd = "grep -P  \"\\tPASS|^#\" $SOURCE  > $TARGET"
    filter_pass_variants = env.Command(["mutect_variants_chr{chr}_pass.vcf".format(**arguments_dictionary)],[mutect2],filter_pass_variants_cmd)



    ####### Annotation by snpEff #########################################
    snpEff_cmd = "java -Xmx4g -jar {snpeffDir}snpEff.jar eff -v {snpeffGenomeVersion} $SOURCE > $TARGET".format(**arguments_dictionary)
    snpEFF = env.Command(["mutect_variants_chr{chr}_pass_snpEFF.vcf".format(**arguments_dictionary)],[filter_pass_variants],snpEff_cmd)

    ##### Clinvar annotation #############################################
    clinvar_cmd = "java -Xmx4g -jar {snpeffDir}SnpSift.jar annotate {annotationDir}{clinvarVCF}  $SOURCE  > $TARGET".format(**arguments_dictionary)
    clinvar = env.Command(["mutect_variants_chr{chr}_pass_snpEFF_clinvar.vcf".format(**arguments_dictionary)], [snpEFF], clinvar_cmd)

    ##### #Cosmic coding variants annotation #############################
    cosmic_coding_cmd = "java -Xmx4g -jar {snpeffDir}SnpSift.jar annotate {annotationDir}{cosmicCodingVCF} $SOURCE  > $TARGET".format(**arguments_dictionary)
    cosmic_coding = env.Command(["mutect_variants_chr{chr}_pass_snpEFF_clinvar_cosmic_coding.vcf".format(**arguments_dictionary)], [clinvar], cosmic_coding_cmd)


    cosmic_non_coding_cmd = "java -Xmx4g -jar {snpeffDir}SnpSift.jar annotate {annotationDir}{cosmicNonCodingVCF}  $SOURCE  > $TARGET".format(**arguments_dictionary)
    cosmic_non_coding = env.Command(["mutect_variants_chr{chr}_pass_snpEFF_clinvar_cosmic_coding_non_coding.vcf".format(**arguments_dictionary)], [cosmic_coding], cosmic_non_coding_cmd)
    vcf_commands.append(cosmic_non_coding)







